apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion buildVersions.compileSdk
    buildToolsVersion buildVersions.buildTools
    defaultConfig {
        applicationId "com.ephemeraldreams.androidbootstrap"
        minSdkVersion buildVersions.minSdk
        targetSdkVersion buildVersions.targetSdk
        versionCode getGitCommitCount("master")
        versionName getGitDescribeTag()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "${rootProject.name}-${versionName}-${versionCode}"
    }
    buildTypes.all {
        it.buildConfigField('String', 'GIT_BRANCH', "\"${getGitBranch()}\"")
        it.buildConfigField('String', 'GIT_COMMIT_HASH', "\"${getGitCommitHash()}\"")
        it.buildConfigField('String', 'GIT_DESCRIBE_TAG', "\"${getGitDescribeTag()}\"")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin libraries.
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}"

    // AndroidX core libraries.
    implementation "androidx.appcompat:appcompat:${versions.androidX}"
    implementation "androidx.core:core-ktx:${versions.androidX}"

    // AndroidX layout libraries.
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"

    // Timber logging library.
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    // LeakCanary memory leak detection library
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}"

    // Testing libraries.
    testImplementation "junit:junit:${versions.junit}"

    // AndroidX testing libraries.
    androidTestImplementation "androidx.test.ext:junit:${versions.androidXJunit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espresso}"
}
