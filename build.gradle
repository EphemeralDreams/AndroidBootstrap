buildscript {
    ext {
        getGitCommitHash = {
            return 'git rev-parse --short HEAD'.execute().text.trim()
        }
        getGitDescribeTag = {
            return 'git describe --tags'.execute().text.trim()
        }
        getGitTag = {
            return 'git describe --tags --abbrev=0'.execute().text.trim()
        }
    }
    ext.versions = [
            'androidTools'    : '3.5.3',
            'androidX'        : '1.1.0',
            'androidXJunit'   : '1.1.1',
            'coroutines'      : '1.3.3',
            'constraintLayout': '1.1.3',
            'espresso'        : '3.2.0',
            'googleJavaFormat': '1.7',
            'junit'           : '4.13',
            'klint'           : '0.36.0',
            'kotlin'          : '1.3.61',
            'leakCanary'      : '2.1',
            'timber'          : '4.7.1'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidTools}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
plugins {
    id "com.diffplug.gradle.spotless" version "3.27.1"
    id "com.github.ben-manes.versions" version "0.27.0"
}
allprojects {
    repositories {
        google()
        jcenter()
    }
}
def isNonStable = { String version ->
    def stableKeyword = [
            'RELEASE', 'FINAL', 'GA'
    ].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
subprojects {
    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        java {
            target "**/*.java"
            googleJavaFormat(versions.googleJavaFormat)
        }
        kotlin {
            target "**/*.kt"
            ktlint(versions.klint)
        }
    }
}
