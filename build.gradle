import com.ephemeraldreams.Deps
import com.ephemeraldreams.Versions

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath(Deps.Android.Gradle)
        classpath(Deps.Kotlin.GradlePlugin)
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
plugins {
    id "com.diffplug.gradle.spotless" version "3.27.1"
    id "com.github.ben-manes.versions" version "0.27.0"
}
allprojects {
    repositories {
        google()
        jcenter()
    }
}
def isNonStable = { String version ->
    def stableKeyword = [
            'RELEASE', 'FINAL', 'GA'
    ].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
subprojects {
    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        java {
            target "**/*.java"
            googleJavaFormat(Versions.googleJavaFormat)
        }
        kotlin {
            target "**/*.kt"
            ktlint(Versions.klint)
        }
    }
}
