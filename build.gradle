// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.versions = [
            'androidTools': '3.5.3',
            'androidX': '1.1.0',
            'androidXJunit': '1.1.1',
            'coroutines': '1.3.0',
            'constraintLayout': '1.1.3',
            'espresso': '3.2.0',
            'junit': '4.12',
            'kotlin': '1.3.61'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidTools}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
plugins {
    id "com.github.ben-manes.versions" version "0.27.0"
}
allprojects {
    repositories {
        google()
        jcenter()
    }
}
def isNonStable = { String version ->
    def stableKeyword = [
            'RELEASE', 'FINAL', 'GA'
    ].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
subprojects {
    dependencyUpdates {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
